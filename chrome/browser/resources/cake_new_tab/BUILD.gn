import("//ui/webui/resources/tools/build_webui.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

fonts = [
  "src/styles/fonts/Inter-Bold.ttf",
  "src/styles/fonts/Inter-Medium.ttf",
  "src/styles/fonts/Inter-Regular.ttf",
  "src/styles/fonts/Inter-SemiBold.ttf"
]

copy("copy_fonts") {
  sources = fonts
  outputs = [ "$target_gen_dir/preprocessed/{{source_target_relative}}" ]
}

fonts_grdp_file = "$target_gen_dir/fonts.grdp"
preprocess_folder = "$target_gen_dir/preprocessed"

generate_grd("build_fonts_grdp") {
  grd_prefix = "fonts"
  deps = [
    ":copy_fonts",
  ]
  input_files_base_dir = rebase_path(preprocess_folder, root_build_dir)
  input_files = []
  foreach(file, fonts) {
    input_files += [ "$file" ]
  }
  out_grd = fonts_grdp_file
}

build_webui("build") {
  grd_prefix = "cake_new_tab"
  webui_context_type = "trusted"

  static_files = [
    "index.html",
    "src/styles/index.css",
  ]

  non_web_component_files = [
    "src/proxy.ts",
    "src/utils/cn.ts",
    "src/utils/url.ts",
    "src/omni/types.ts",
    "src/omni/Omni.tsx",
    "src/omni/SearchResult.tsx",
    "src/pages/types.ts",
    "src/pages/EntryPage.tsx",
    "src/pages/PageManager.tsx",
    "src/App.tsx",
    "src/index.tsx",
  ]

  mojo_files_deps = [
    "//chrome/browser/ui/webui/cake_new_tab:mojo_bindings_ts__generator",
  ]

  mojo_files = [
    "$root_gen_dir/chrome/browser/ui/webui/cake_new_tab/cake_new_tab.mojom-webui.ts",
  ]

  ts_deps = [ 
    "//third_party/cake_ui:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]

  ts_path_mappings = [
    "resources/cake_ui/ui.rollup.js|../../../../third_party/cake_ui/ui.d.ts",
  ]

  extra_grdp_deps = [
    ":build_fonts_grdp",
  ]

  extra_grdp_files = [
    fonts_grdp_file,
  ]
}
